plugins {
    id 'groovy'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.11.0'
    id 'nebula.lint' version '16.6.0'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'ru.vyarus.quality' version '4.7.0'
    id 'com.bmuschko.clover' version '3.0.2'
}

apply plugin: 'idea'

description = 'Gradle plugin to integrate with AWS ECR'
group = 'com.patdouble'
version = 'SNAPSHOT'

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

pluginBundle {
  website = 'https://bitbucket.org/double16/gradle-aws-ecr-plugin'
  vcsUrl = 'https://bitbucket.org/double16/gradle-aws-ecr-plugin.git'
  description = project.description
  tags = ['container', 'image', 'linux', 'vm', 'docker', 'lightweight', 'aws', 'ecr']

  plugins {
    awsecrPlugin {
      id = 'com.patdouble.awsecr'
      displayName = 'AWS ECR Configuration Plugin'
    }
  }
}

// Set product version if tag is given on CI
System.getenv('CIRCLE_TAG')?.eachMatch(/^[0-9]+(\.[0-9]+)*$/) { m -> project.version = m[0] }

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    jcenter()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation 'com.bmuschko:gradle-docker-plugin:6.7.0'
    implementation 'com.amazonaws:aws-java-sdk-ecr:1.12.116'
    testImplementation('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testImplementation 'cglib:cglib-nodep:3.3.0'
    clover 'org.openclover:clover:4.4.1'
}

tasks.publishPlugins.doFirst {
    assert !project.version.contains('SNAPSHOT')
}

clover {
    report {
        xml = true
        html = true
    }
}

afterEvaluate {
    codenarc {
        maxPriority1Violations = 0
        maxPriority2Violations = 5
        maxPriority3Violations = 50
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId rootProject.group
            artifactId rootProject.name
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = DELEGATE_FIRST
                    name rootProject.name
                    description rootProject.description
                }
            }
        }
    }
}

task install(dependsOn: 'publishToMavenLocal')

