plugins {
    id 'com.gradle.build-scan' version '1.12.1'
    id 'groovy'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.9.10'
    id 'nebula.lint' version '9.0.0'
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'ru.vyarus.quality' version '2.4.0'
    id 'com.bmuschko.clover' version '2.2.0'
    id 'org.sonarqube' version '2.6.2'
}

apply plugin: 'idea'

description = 'Gradle plugin to integrate with AWS ECR'
group = 'com.patdouble'
version = 'SNAPSHOT'

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

pluginBundle {
  website = 'https://bitbucket.org/double16/gradle-aws-ecr-plugin'
  vcsUrl = 'https://bitbucket.org/double16/gradle-aws-ecr-plugin.git'
  description = project.description
  tags = ['container', 'image', 'linux', 'vm', 'docker', 'lightweight', 'aws', 'ecr']

  plugins {
    awsecrPlugin {
      id = 'com.patdouble.awsecr'
      displayName = 'AWS ECR Configuration Plugin'
    }
  }
}

// Set product version if tag is given on CI
System.getenv('CIRCLE_TAG')?.eachMatch(/^[0-9]+(\.[0-9]+)*$/) { m -> project.version = m[0] }

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.bmuschko:gradle-docker-plugin:3.2.5'
    compile 'com.amazonaws:aws-java-sdk-ecr:1.11.297'
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testCompile 'cglib:cglib-nodep:3.2.6'
    clover 'org.openclover:clover:4.2.1'
}

tasks.publishPlugins.doFirst {
    assert !project.version.contains('SNAPSHOT')
}

clover {
    report {
        xml = true
        html = true
    }
}

sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', System.getenv('SONARQUBE_ORG')
        property 'sonar.login', System.getenv('SONARQUBE_LOGIN')
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId rootProject.group
            artifactId rootProject.name
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = DELEGATE_FIRST
                    name rootProject.name
                    description rootProject.description
                }
            }
        }
    }
}

task install(dependsOn: 'publishToMavenLocal')

