plugins {
    id 'com.gradle.build-scan' version '2.1'
    id 'groovy'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'nebula.lint' version '10.4.2'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'ru.vyarus.quality' version '3.3.0'
    id 'com.bmuschko.clover' version '2.2.1'
    id 'org.sonarqube' version '2.7'
}

apply plugin: 'idea'

description = 'Gradle plugin to integrate with AWS ECR'
group = 'com.patdouble'
version = 'MSNAPSHOT'

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

pluginBundle {
  website = 'https://bitbucket.org/double16/gradle-aws-ecr-plugin'
  vcsUrl = 'https://bitbucket.org/double16/gradle-aws-ecr-plugin.git'
  description = project.description
  tags = ['container', 'image', 'linux', 'vm', 'docker', 'lightweight', 'aws', 'ecr']

  plugins {
    awsecrPlugin {
      id = 'com.patdouble.awsecr'
      displayName = 'AWS ECR Configuration Plugin'
    }
  }
}

// Set product version if tag is given on CI
System.getenv('CIRCLE_TAG')?.eachMatch(/^[0-9]+(\.[0-9]+)*$/) { m -> project.version = m[0] }

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.bmuschko:gradle-docker-plugin:4.4.0'
    compile 'com.amazonaws:aws-java-sdk-ecr:1.11.491'
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
    }
    testCompile 'cglib:cglib-nodep:3.2.10'
    clover 'org.openclover:clover:4.3.1'
}

tasks.publishPlugins.doFirst {
    assert !project.version.contains('SNAPSHOT')
}

clover {
    report {
        xml = true
        html = true
    }
}

sonarqube {
    properties {
        property 'sonar.host.url', System.getenv('SONARQUBE_URL') ?: 'https://sonarcloud.io'
        property 'sonar.organization', System.getenv('SONARQUBE_ORG')
        property 'sonar.login', System.getenv('SONARQUBE_LOGIN')
        property 'sonar.clover.reportPath', "${cloverGenerateReport.reportsDir.absolutePath}/clover/clover.xml"
    }
}
tasks.sonarqube.dependsOn check, cloverGenerateReport

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId rootProject.group
            artifactId rootProject.name
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = DELEGATE_FIRST
                    name rootProject.name
                    description rootProject.description
                }
            }
        }
    }
}

task install(dependsOn: 'publishToMavenLocal')

